<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8">
<script id="sphere-vertex-shader" type="x-shader/x-vertex">
precision mediump float;
attribute vec4 vPosition;

uniform vec3 center;
uniform float radius;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;

varying vec3 fCenter;
varying vec3 fPosition;
varying float fRadius;
varying vec3 E;
varying vec3 L;


vec3 orthagonalSquare(vec3 center, float vertexId) {
  vec3 v = vec3(0.0, 0.0, 0.0) - center;         // vector from center to eye
  vec3 corner;                                    // vector to square corner
  corner = vec3(1, 1, -1.0 * (v.x + v.y) / v.z);  // vector orthagonal to v
  if(vertexId == 1.0){
    corner = cross(v, corner);              // orthagonal to v and coplanar to previous vPrime
  } else if (vertexId == 2.0){
    corner = cross(v, corner);
    corner = cross(v, corner);              // orthagonal to v and coplanar to previous vPrimes
  } else if (vertexId == 3.0){
    corner = cross(v, corner);
    corner = cross(v, corner);
    corner = cross(v, corner);              // orthagonal to v and coplanar to previous vPrimes
  }
  return corner;
}

void main() {
  // modelview center
  vec3 mvCenter = (modelViewMatrix * vec4(center, 1.0)).xyz;
  // calculate square orthagonal to the eye
  vec3 cornerV = orthagonalSquare(mvCenter, vPosition.x);
  // modelview vertex
  vec4 mvV = vec4(mvCenter + normalize(cornerV)*radius*sqrt(2.0), 1.0);

  gl_Position = projectionMatrix * mvV;

  // calc fragment shader parameters (ignore projection matrix)
  fRadius = length( normalize(cornerV)*radius );      // modelview radius
  fPosition = mvV.xyz;                                // modelveiw vertex
  fCenter = mvCenter;                                 // modelview cube center
  E =  - (mvV).xyz;                                   // position to eye
  L = (modelViewMatrix * lightPosition).xyz - mvV.xyz;// position of light
}
</script>

<script id="sphere-fragment-shader" type="x-shader/x-fragment">
precision mediump float;

varying vec3 fCenter;
varying vec3 fPosition;
varying float fRadius;
varying vec3 E;
varying vec3 L;

uniform float ka;
uniform float kd;
uniform float ks;
uniform float shininess;
uniform samplerCube texMap;
uniform mat4 modelViewMatrix;
uniform mat4 invMV;
uniform mat4 projectionMatrix;
uniform sampler2D texture;

#ifdef GL_EXT_frag_depth
	#extension GL_EXT_frag_depth : enable
#endif

void
main()
{
  vec4 fColor = vec4(1.0, 0.0, 0.0, 1.0);

  // calc normal based on mvPosition and mvCenter
  vec3 dif = fPosition - fCenter;
  float distance = length(dif);              // distance from the center of the circle
  if (distance > fRadius) {
    // point is outside of the circle
		discard;
    return;
  }
  float x = fPosition.x - fCenter.x;
  float y = fPosition.y - fCenter.y;
  float z = sqrt(fRadius*fRadius - x * x - y * y);
  #ifdef GL_EXT_frag_depth
    gl_FragDepthEXT = gl_FragCoord.z;
  #endif

  vec3 NN = normalize(vec3(x, y, z));
  vec3 LL = normalize(L);
  vec3 EE = normalize(E);
  vec3 HH = normalize( LL + EE );

  /**************** 2D TEXTURE ****************/
  /* texture parameters */
  float pi = radians(180.0);

  // Convert fragment position to object coordinates
  vec3 texVec = ( invMV * vec4(fCenter + vec3(x, y, z), 1.0)).xyz;
  // Convert sphere center positionn to object coordinates
  vec3 tempCenter = (invMV * vec4(fCenter, 1.0)).xyz;
  // Vector from sphere surface position to sphere center
  texVec = texVec - tempCenter;
  texVec = normalize(texVec);

  // Calculate the s and t coordinates for texture mapping
  float sCoord = 0.5 + (atan(texVec.x,texVec.z))/(2.0*pi);
  float tCoord = 0.5 - asin(texVec.y)/(pi);
  vec2 texCoord = vec2(sCoord, tCoord);

  fColor = texture2D( texture,  texCoord);
  /************** END 2D TEXTURE **************/

  // calculate phong shading elements
  float ambient = ka;
  float diffuse = kd * max( dot(NN, LL), 0.0 );
  float specular = ks * pow( max(dot(NN, HH), 0.0), shininess );
  if( dot(LL, NN) < 0.0 ) specular = 0.0;

  // apply phong shading to color
  fColor = fColor * (ambient + diffuse) + vec4(1.0) * specular;
  fColor.a = 1.0;
  gl_FragColor = fColor;

}
</script>
<script type="text/javascript" src="../common/webgl-utils.js"></script>
<script type="text/javascript" src="../common/initShaders.js"></script>
<script type="text/javascript" src="../common/MV.js"></script>
<script type="text/javascript" src="../common/camera.js"></script>
<script type="text/javascript" src="../common/shading.js"></script>
<script type="text/javascript" src="bounce_n_break.js"></script>
<link rel="stylesheet" href="../common/fullscreen.css">
</head>

<body>
<canvas id="gl-canvas">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
</body>
</html>
